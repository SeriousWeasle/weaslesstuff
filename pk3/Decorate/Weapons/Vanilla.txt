ACTOR GreenShellsLoadedSSG : Inventory
{
    Inventory.MaxAmount 1
}

ACTOR VanillaSuperShotgun : SuperShotgun
{
    Weapon.SlotNumber 3
    Weapon.AmmoUse1 0
    Weapon.AmmoGive1 8
    Weapon.AmmoType1 "Shell"
    Weapon.AmmoUse2 0
    Weapon.AmmoGive2 12
    Weapon.AmmoType2 "ShellAuto"
    States
    {
        Ready:
            SHT2 A 1 A_WeaponReady (WRF_ALLOWRELOAD)
            Loop
        Fire:
            SHT2 A 0 A_JumpIfInventory("GreenShellsLoadedSSG", 1, "GreenFireCheck")
        RedFireCheck:
            SHT2 A 0 A_JumpIfInventory("Shell", 2, "RedFire")
            Goto Ready
        RedFire:
            SHT2 A 3 A_TakeInventory("Shell", 2)
            SHT2 A 0 A_FireBullets (11.2, 7.1, 20, 5, "BulletPuff")
            SHT2 A 0 A_PlaySound ("weapons/sshotf", CHAN_WEAPON)
            SHT2 A 7 A_GunFlash
            SHT2 B 7
            SHT2 C 7 A_CheckReload
            SHT2 D 7 A_OpenShotgun2
            SHT2 E 7
            SHT2 F 7 A_LoadShotgun2
            SHT2 G 6
            SHT2 H 6 A_CloseShotgun2
            SHT2 A 5 A_ReFire
            Goto Ready
        GreenFireCheck:
            SHT2 A 0 A_JumpIfInventory("ShellAuto", 2, "GreenFire")
            Goto Ready
        GreenFire:
            SHT2 A 3 A_TakeInventory("ShellAuto", 2)
            SHT2 A 0 A_FireBullets (7.4, 5.4, 20, 3, "BulletPuff")
            SHT2 A 0 A_PlaySound ("weapons/sshotf", CHAN_WEAPON)
            SHT2 A 7 A_GunFlash
            SHT2 B 7
            SHT2 C 7 A_CheckReload
            SHT2 D 7 A_OpenShotgun2
            SHT2 K 7
            SHT2 L 7 A_LoadShotgun2
            SHT2 G 6
            SHT2 H 6 A_CloseShotgun2
            SHT2 A 5 A_ReFire
            Goto Ready
        Reload:
            SHT2 A 0 A_JumpIfInventory("GreenShellsLoadedSSG", 1, "LoadRed")
        LoadGreen:
            SHT2 A 0 A_GiveInventory("GreenShellsLoadedSSG", 1)
            SHT2 H 6 A_OpenShotgun2
            SHT2 G 6
            SHT2 F 7 A_LoadShotgun2
            SHT2 E 7
            SHT2 M 7
            SHT2 K 7 
            SHT2 L 7 A_LoadShotgun2
            SHT2 G 6
            SHT2 H 6 A_CloseShotgun2
            Goto Ready
        LoadRed:
            SHT2 A 0 A_TakeInventory("GreenShellsLoadedSSG", 1)
            SHT2 H 6 A_OpenShotgun2
            SHT2 G 6
            SHT2 L 7 A_LoadShotgun2
            SHT2 K 7 
            SHT2 M 7
            SHT2 E 7 
            SHT2 F 7 A_LoadShotgun2
            SHT2 G 6
            SHT2 H 6 A_CloseShotgun2
            Goto Ready
    }
}

ACTOR VanillaShotgun : Shotgun
{
    Weapon.SlotNumber 3
    Weapon.AmmoUse1 1
    Weapon.AmmoGive1 4
    Weapon.AmmoType1 "Shell"
    Weapon.AmmoUse2 1
    Weapon.AmmoGive2 8
    Weapon.AmmoType2 "ShellAuto"
    States
    {
        Fire:
            SHTG A 3
            SHTG A 0 A_FireBullets (5.6, 0, 7, 5, "BulletPuff")
            SHTG A 0 A_PlaySound ("weapons/shotgf", CHAN_WEAPON)
            SHTG A 7 A_GunFlash
            SHTG BC 5
            SHTG D 4
            SHTG CB 5
            SHTG A 3
            SHTG A 7 A_ReFire
            Goto Ready
        AltFire:
            SHTG A 3
            SHTG A 0 A_FireBullets (3.6, 0, 7, 3, "BulletPuff")
            SHTG A 0 A_PlaySound ("weapons/shotgf", CHAN_WEAPON)
            SHTG A 7 A_GunFlash
            SHTG BC 5
            SHTG D 4
            SHTG CB 5
            SHTG A 3
            SHTG A 7 A_ReFire
            Goto Ready
    }
}

ACTOR VanillaChaingun : Chaingun
{
    Weapon.SlotNumber 4
}

ACTOR VanillaPlasmaRifle : PlasmaRifle
{
    Weapon.SlotNumber 6
}

ACTOR VanillaBFG : BFG9000
{
    Weapon.SlotNumber 7
}