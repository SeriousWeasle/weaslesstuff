ACTOR MachinePistolAmount : Inventory
{
    Inventory.MaxAmount 1
}

ACTOR MachinePistolGiver : CustomInventory
{
    Inventory.PickupMessage "Got the machine pistol"
    Inventory.PickupSound "misc/w_pkup"
    States
    {
        Spawn:
            MPST D -1
            Stop
        Pickup:
            TNT1 A 0 A_JumpIfInventory("MachinePistolAmount", 1, "GiveSecond")
            TNT1 A 0 A_GiveInventory("MachinePistol", 1)
            TNT1 A 0 A_GiveInventory("MachinePistolAmount", 1)
            Stop
        GiveSecond:
            TNT1 A 0 A_GiveInventory("MachinePistol", 1)
            TNT1 A 0 A_GiveInventory("DoubleMachinePistol", 1)
            Stop
    }
}

ACTOR MachinePistol : DoomWeapon
{
    Weapon.SlotNumber 2
    Weapon.AmmoUse1 1
    Weapon.AmmoGive1 50
    Weapon.AmmoType1 "Clip5mm"
    Weapon.AmmoUse2 1
    Weapon.AmmoType2 "Clip5mm"
    Inventory.PickupMessage "Got the machine pistol"
    States
    {
        Ready:
            MPST A 1 A_WeaponReady
            Loop
        Deselect:
            MPST A 0 A_PlaySound("12mmPistol/Lower", 0)
            Goto Lower
        Lower:
            MPST A 1 A_Lower
            Loop
        Select:
            MPST A 0 A_PlaySound("12mmPistol/Raise", 0)
            Goto Raise
        Raise:
            MPST A 1 A_Raise
            Loop
        Fire:
            MPST A 0 A_PlaySound ("SMG/Fire", 0)
            MPST B 1 A_FireBullets(3.2, 0, 1, 6, "BulletPuff")
            MPST C 1 A_WeaponOffset(10, 10, WOF_ADD|WOF_INTERPOLATE)
            MPST A 1 A_WeaponOffset(-2, -2, WOF_ADD|WOF_INTERPOLATE)
            MPST A 1 A_WeaponOffset(-3, -3, WOF_ADD|WOF_INTERPOLATE)
            MPST A 1 A_WeaponOffset(-5, -5, WOF_ADD|WOF_INTERPOLATE)
            MPST A 1 A_ReFire
            Goto Ready
        AltFire:
            MPST A 0 A_PlaySound ("SMG/Fire", 0)
            MPST B 1 A_FireBullets(4.8, 0, 1, 6, "BulletPuff")
            MPST C 1 A_WeaponOffset(5, 5, WOF_ADD|WOF_INTERPOLATE)
            MPST A 1 A_WeaponOffset(-2.5, -2.5, WOF_ADD|WOF_INTERPOLATE)
            MPST A 1 A_WeaponOffset(-2.5, -2.5, WOF_ADD|WOF_INTERPOLATE)
            MPST A 1 A_ReFire
            Goto Ready
        Spawn:
            MPST D -1
            Stop
    }
}

ACTOR DoubleMachinePistol : DoomWeapon
{
    Weapon.SlotNumber 2
    Weapon.AmmoUse1 2
    Weapon.AmmoGive1 0
    Weapon.AmmoType1 "Clip5mm"
    Weapon.AmmoUse2 2
    Weapon.AmmoType2 "Clip5mm"
    Inventory.PickupMessage "Got the machine pistol"
    States
    {
        Ready:
            MPTD A 1 A_WeaponReady
            Loop
        Deselect:
            MPTD A 0 A_PlaySound("12mmPistol/Lower", 0)
            Goto Lower
        Lower:
            MPTD A 1 A_Lower
            Loop
        Select:
            MPTD A 0 A_PlaySound("12mmPistol/Raise", 0)
            Goto Raise
        Raise:
            MPTD A 1 A_Raise
            Loop
        Fire:
            MPTD A 0 A_PlaySound ("SMG/Fire", 0)
            MPTD B 1 A_FireBullets(4.8, 0, 2, 6, "BulletPuff")
            MPTD G 1
            MPTD H 1
            MPTD A 2
            MPTD A 1 A_ReFire
            Goto Ready
        AltFire:
            MPTD A 0 A_PlaySound ("SMG/Fire", 0)
            MPTD B 1 A_FireBullets(7.2, 0, 2, 6, "BulletPuff")
            MPTD G 1
            MPTD H 1
            MPTD A 1
            MPTD A 1 A_ReFire
            Goto Ready
        Spawn:
            MPST I -1
            Stop
    }
}